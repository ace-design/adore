/** GDR GPL 2010, ADORE Demonstration
 * @author: Sebastien Mosser, Mireille Blay-Fornarino
 * @related: TAOSD Special Issue on Aspect Oriented Modeling
 **/


/** Requirement Description (UC #2.2 Main Success Scenario):
 *
 * Coordinator requests Witness to provide his identification.
 * 1. Coordinator provides witness information1 to System as reported 
 *    by the witness.
 * 2. Coordinator informs System of location and type of crisis as reported 
 *    by the witness.
 * In parallel to steps 2-4:
 *      2a.1 System contacts PhoneCompany to verify witness information.
 *      2a.2 PhoneCompany sends address/phone information to System.
 *      2a.3 System validates information received from the PhoneCompany.
 * 3. System provides Coordinator with a crisis-focused checklist.
 * 4. Coordinator provides crisis information2 to System as reported by the 
 *    witness.
 * 5. System assigns an initial emergency level to the crisis and sets the 
 *    crisis status to active.
 * Use case ends in success.
 *
 **/

orchestration cms::captureWitnessReport { 
  variables {
    id as crisisIdentifier; coord as cmsEmployee;
    wi as witness; i as preliminaryInformation;
    ccl as crisisCheckList; ci as crisisInformation;
    pi as phoneInformation; exact as boolean;
  }
  activities {
    rcv. (coord, id) := receive();
    a10. wi := ui::promptWitnessInfo(coord);
    a11. cms::setWitness(wi,id);
    a2. i := ui::promptPrelimInfo(coord);
    a2a12. pi := phoneCie::getInfo(wi.id);
    a2a3. exact := cms::validateWitnessInfo(wi,pi);
    a3. ccl := cms::buildCheckList(i);
    a4. ci := ui::promptCheckList(coord,id, ccl);
    a50. cms::assignEmergencyLvl(id,ci);
    a51. msgBus::send('status' as string, 'active' as string,id);
    rpl. reply();
  }
  relations {
    rcv < a10; a10 < a11; a10 < a2; a2 < a3; a3 < a4; a4 < a50; 
    a50 < rpl; a10 < a2a12; a2a12 < a2a3; a2a3 < a50; a50 < a51;
  }
}
