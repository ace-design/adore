/** GDR GPL 2010, ADORE Demonstration
 * @author: Sebastien Mosser, Mireille Blay-Fornarino
 * @related: TAOSD Special Issue on Aspect Oriented Modeling
 **/


/*************************************************
 ** Business Extensions (scenario alternatives) **
 *************************************************/

/** Extension #1a, #2a: 
 * The call is disconnected. The base use case terminates.
 **/ 
fragment callDisconnected {
  variables {}
  activities { h. hook(); thr. throw('lostWitnessContact' as string); }
  relations { ^ < h; h < $; fail(h,'disconnected') < thr; }
}
depict callDisconnected using 'gold';



/** Extension #3a:
 * In parallel to steps 3-4, 
 *    if the crisis location is covered by camera surveillance:
 *      3a.1 System requests video feed from SurveillanceSystem.
 *      3a.2 SurveillanceSystem starts sending video feed to System.
 *      3a.3 System starts displaying video feed for Coordinator.
 **/
fragment requestVideo<user> {
  variables {
    c as boolean; info as preliminaryInformation;
    user as cmsEmployee; feedId as videoFeedIdentifier;
  }
  activities {
    t. c := survSys::canCover(info.loc);
    h. hook(info);
    a12. feedId := survSys::activateVideo(info.loc);
    a3. ui::displayVideoFeed(user, feedId); 
  }
  relations { ^ < h; h < $; ^ < t; t < a12 when c; a12 < a3; }
}
depict requestVideo using 'skyblue';


/** Extension #4a:
 * The call is disconnected.
 *   4a.1 Use case continues at step 5 without crisis information.
 **/
fragment ignoreDisconnection {
  variables  {}
  activities { h. hook(); }
  relations  { ^ < h; h < $; fail(h,'disconnected') < $; }
}


/** Extension #5a: 
 * PhoneCompany information does not match information received from Witness.
 *   5a.1 The base use case is terminated.
 **/
fragment fakeWitnessInfo {
  variables { 
    witInfo as witness; phInfo as phoneInformation; isValid as boolean;
  }
  activities {
    h. isValid := hook(witInfo,phInfo); 
    thr. throw('fakeWitnessInformation' as string);
  }
  relations { ^ < h; h < $ when isValid; h < thr when ! isValid; }
}
depict fakeWitnessInfo using 'burlywood';


/** Extension #5b:
 * Camera vision of the location is perfect, but Coordinator cannot confirm 
 * the situation that the witness describes or the Coordinator determines that 
 * the witness is calling in a fake crisis.
 *   5b.1 The base use case is terminated.
 **/
fragment fakeCrisisDetected {
  variables { user as cmsEmployee; isReal as boolean; }
  activities {
    h. hook(user);
    t. isReal := ui::prompt4RealCrisis(user);
    thr. throw('fakeCrisis' as string);
  }
  relations { ^ < h; h < $; ^<t; t < $ when isReal; t < thr when ! isReal; }
}
depict fakeCrisisDetected using 'darkseagreen3' ;


/***********************************************
 ** Non Functional Concern: Statistic Logging **
 ***********************************************/

/** Description:
 * - The system shall record the following statistical information on both 
 *   on-going and resolved crises: rate of progression; average response time 
 *   of rescue teams; individual response time of each rescue team; success
 *    rate of each rescue team; rate of casualties; success rate of missions.
 *  - The system shall provide statistical analysis tools to analyse 
 *    individual crisis data and data on multiple crises.
 * Realization in ADORE:
 *  -> logTime:  fragment to store execution time of operation
 *  -> logError: fragment to store exceptional ending of operation
 **/

fragment logTime<keyType, op> {
  variables { 
    key as keyType; start as long ; end as long; delta as long; op as string; 
  }
  activities {
    a1.  start := system_currentTimeMillis(); 
    h.  hook(key);
    a2. end := system_currentTimeMillis();
    a3. delta := diff(end,start);
    a4. dataBase::storeExecTime(key,op, delta);
  }
  relations {
    ^<a1; a1<h; h<a2; a2<a3; a3<a4; a2<$; 
  }
}
depict logTime using 'lightpink3';

fragment logError<exc, msg, op>{
  variables { msg as string; op as var; }
  activities {
    h.  hook();
    a0. database::storeError(msg,op);
  }
  relations {
     ^<h; fail(h,'exc')<a0; h<$;
  }
}
depict logError using 'tomato2';



/**************************************
 ** Non Functional Concern: Security **
 **************************************/

/** Description:
 * - The system shall define access policies for various classes of users. 
 *   The access policy shall describe the components and information each 
 *   class may add, access and update.
 * - The system shall authenticate users on the basis of the access policies 
 *   when they first access any components or information. If a user remains 
 *   idle for 30 minutes or longer, the system shall require them to 
 *   re-authenticate.
 * - All communications in the system shall use secure channels compliant with 
 *   AES-128 standard encryption.
 * ADORE Realization: Focus on point 2, wich is behavioral.
 *   -> authentifyWhenIdle fragment
**/

fragment authentifyWhenIdle {
  variables { u as cmsEmployee; still as boolean; auth as boolean; }
  activities {
    a1. still :=  cms::notIdle(u);
    h. hook(u);
    a21. cms::killAuthentification(u);
    a2. auth := cms::authUser(u);
    t. throw('BadAuthentification' as string);
  }
  relations {
    ^ < a1; a1 < h when still; h < $; a1 < a21 when ! still; a21<a2;
    a2 < h when auth; a2 < t when ! auth;
  }
}
depict authentifyWhenIdle using 'tan3';



/***************************************************
 ** Non Functional Concern: Persistence (Extract) **
 ***************************************************/

/** Description:
 * - The system shall provide support for storing, updating and accessing the 
 *   following information on available and deployed resources (both internal 
 *   and external): type of resource (human or equipment); capability; rescue 
 *   team; location; estimated time of arrival (ETA) on crisis site.
 * ADORE Realization:
   -> logUpdate fragment to keep track of resource status
**/

fragment logUpdate<vType, op> {
  variables { v as vType;  op as string;  }
  activities {
    h.  hook(v);
    a2. crud::update(v, op);
  }
  relations {
   ^<h ; h <a2; h< $; 
  }
}
depict logUpdate using 'lavender';
